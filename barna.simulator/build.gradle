apply plugin: 'groovy'

dependencies{
    compile "jdbm:jdbm:2.3"
    compile project(":barna.model")
    compile project(":barna.io")
    compile project(":barna.commons")
    groovy group: 'org.codehaus.groovy', name: 'groovy-all', version: '1.8.4'
}

def outputName = "flux-simulator-${version}"
ext.distSpec = copySpec {
    from(project.rootDir){
        include 'LICENSE'
        include 'LGPL_LICENSE.txt'
        include 'APACHE_LICENSE.txt'
    }
    from('src/main/distribution/'){
        include 'README.txt'
    }
    into('bin') {
        from("${project.rootDir}/barna.distribution/src/main/dist/bin")
        filter(org.apache.tools.ant.filters.ReplaceTokens, tokens:
            [
                "flux.tool":'simulator',
                "flux.app":project.name
            ]
        )

        rename 'flux-template.sh',  'flux-simulator'
        rename 'flux-template.bat', 'flux-simulator.bat'
        fileMode = 0755
    }    
    into('lib') {
        from(jar.outputs.files)
        from(configurations.runtime)        
    }
}

task distDir(type: Copy){
    description = "Prepare a directory for distribution"
    into "${distsDir}/${outputName}"
    with distSpec
}


task distTar(type: Tar) {
    description = "Tars the binary distribution."
    baseName "flux-simulator"
    into "${outputName}"    
    with distSpec
    compression = Compression.GZIP
}

task distZip(type: Zip) {
    description = "Zip the binary distribution."
    baseName "flux-simulator"
    into "${outputName}"
    with distSpec
}

task dist(dependsOn: ['distTar', 'distZip'])


/**
 * Create aggregated javaDoc from all subprojects
 */
task aggregatedJavadoc(type: Javadoc){
    description = "Build the aggregated JavaDocs for all simulator modules"
    source = [project(":barna.model"), project(":barna.io"), project(":barna.commons"),project(":barna.simulator") ].collectAll{p-> p.sourceSets.main.java}
    destinationDir = file("${buildDir}/docs/flux-simulator-${version}")
    configure(options) {
        windowTitle = 'Flux-Simulator API Documentation'
        docTitle = "Flux-Simulator JavaDoc (${project(':barna.simulator').version})"
        links = [ 'http://download.oracle.com/javase/6/docs/api/',
                'http://download.oracle.com/javaee/6/api/' ]
    }
}

task zipJavadoc(type: Zip, dependsOn: aggregatedJavadoc ){
    description = "Zip the aggregated javadoc"
    classifier = "javadoc"
    baseName = "flux-simulator"
    from("${buildDir}/docs/flux-simulator-${version}"){
        if(version =~ /.*SNAPSHOT.*/){
            into "flux-simulator-DEVEL"
        }else{
            into "flux-simulator-${version}"
        }
    }
}



artifacts{
    archives distTar 
}
